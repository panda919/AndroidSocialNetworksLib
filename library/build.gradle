apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

version = "0.3.7"
group = "com.github.androidsocialnetworks"

android {
    packagingOptions {
        exclude 'jaxb.properties'
    }

    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0@jar'
    compile 'org.twitter4j:twitter4j-core:4.0.1'
    compile 'com.google.android.gms:play-services:4.3.23@aar'
    compile 'com.github.androidsocialnetworks:facebook:3.14.2@aar'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Android Social Networks Library'
            packaging 'aar'
            description 'Library for easy work with social networks on Android'
            url 'https://github.com/antonkrasov/AndroidSocialNetworks'

            scm {
                url 'scm:git@github.com:antonkrasov/AndroidSocialNetworks'
                connection 'scm:git@github.com:antonkrasov/AndroidSocialNetworks.git'
                developerConnection 'scm:git@github.com:antonkrasov/AndroidSocialNetworks.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'anton.krasov'
                    name 'Anton Krasov'
                    email 'anton.krasov@gmail.com'
                }
            }
        }
    }
}